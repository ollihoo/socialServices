plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'de.hoogvliet'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.47.2.0'
    implementation 'org.hibernate.orm:hibernate-community-dialects'
    implementation 'org.jsoup:jsoup:1.15.4'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.4.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

def static runGitCommand(String command) {
    def process = command.execute()
    def output = new StringBuffer()
    def error = new StringBuffer()
    process.consumeProcessOutput(output, error)
    process.waitFor()

    if (process.exitValue() != 0) {
        throw new GradleException("git command failed: ${error}")
    }

    return output.toString().trim()
}

def incrementVersion() {
    def versionFile = file('version.properties')
    Properties properties = new Properties()
    versionFile.withReader { reader ->
        properties.load(reader)
    }
    String currentVersion = properties['version'] ?: '0.0.0'
    def (major, minor, patch) = currentVersion.split('\\.').collect { it.toInteger() }
    patch += 1
    String newVersion = "${major}.${minor}.${patch}"
    properties['version'] = newVersion
    versionFile.withWriter { writer ->
        properties.store(writer, null)
    }
    runGitCommand("git commit -m'New build with version number ${newVersion}' .")
    return newVersion
}

if (!project.hasProperty('release')) {
    version = file('version.properties').withReader {
        Properties props = new Properties()
        props.load(it)
        props['version']
    }
} else {
    version = incrementVersion()
}
